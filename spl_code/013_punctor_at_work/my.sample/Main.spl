/*
This example shows how a Punctor operator could be used in an application.
Punctor operator allows the transformation of the input tuples and then
inject puncuation markers either before or after the output tuple as cconfigured.
*/
namespace my.sample;

composite Main {
	type
		Student = tuple <rstring name, int32 id, int32 rank, rstring major>;
	
	graph
		stream <Student> StudentRecord = Beacon() {
			logic
				state: mutable int32 n = 0;
			
			param
				iterations: 100u;
			
			output
				StudentRecord: name = "Name" + (rstring)++n, id = n+1000, rank = (n%5 == 0) ? 5 : n%5, major = "Major" + (rstring)n;
		} // End of Beacon().
		
		// We are going to configure the Punctor operator to send punctuation
		// markers for specific student ids.
		stream <Student> PunctorControlledStudentRecord = Punctor(StudentRecord) {
			param
				punctuate: (id == 1026 || id == 1051 || id == 1076 || id >= 1100);
				position: before;
		} // End of Punctor(StudentRecord)
		
		() as ScreenWriter1 = Custom(PunctorControlledStudentRecord) {
			logic
				state: {
					mutable boolean punctorArrived = false; 
					mutable int32 err = 0;
				} // End of state.
				
				onPunct PunctorControlledStudentRecord: 
					punctorArrived = true;
				
				onTuple PunctorControlledStudentRecord: 
					if (punctorArrived) {
						punctorArrived = false;
						printString ("Punctuation arrived before->" + (rstring) PunctorControlledStudentRecord + "\n");	
						spl.file::fflush (0ul, err);
					} // End of if (punctorArrived)
		} // End of Custom(PunctorControlledStudentRecord)
} // End of composite Main.
